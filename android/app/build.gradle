plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("android/app/key.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace = "com.example.flow_ai"
    compileSdk = 36
    ndkVersion = flutter.ndkVersion
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId = "ai.flow.app"
        minSdkVersion flutter.minSdkVersion
        targetSdk = 35
        versionCode = flutter.versionCode
        versionName = flutter.versionName

        def props = new Properties()
        def propsFile = rootProject.file("local.properties")
        if (propsFile.exists()) {
            props.load(new FileInputStream(propsFile))
        }
        def supabaseUrl = props.getProperty("SUPABASE_URL", "https://mesxkddjgnmmlwsxzxgo.supabase.co")
        def supabaseAnon = props.getProperty("SUPABASE_ANON_KEY", "eyJhbGciOiJIUzI1NiIsInR5cCI6IkpXVCJ9.eyJpc3MiOiJzdXBhYmFzZSIsInJlZiI6Im1lc3hrZGRqZ25tbWx3c3h6eGdvIiwicm9sZSI6ImFub24iLCJpYXQiOjE3NTQ3NTgzODQsImV4cCI6MjA3MDMzNDM4NH0.sitPOrrlf6KiIuSDjwz4wezrkm2nR65_Mts4op5Os-c")
        buildConfigField "String", "SUPABASE_URL", '"' + supabaseUrl + '"'
        buildConfigField "String", "SUPABASE_ANON_KEY", '"' + supabaseAnon + '"'
    }

    signingConfigs {
        release {
            keyAlias System.getenv("RELEASE_KEY_ALIAS") ?: keystoreProperties['keyAlias']
            keyPassword System.getenv("RELEASE_KEY_PASSWORD") ?: keystoreProperties['keyPassword']
            storePassword System.getenv("RELEASE_STORE_PASSWORD") ?: keystoreProperties['storePassword']
            storeFile System.getenv("RELEASE_STORE_FILE") ? file(System.getenv("RELEASE_STORE_FILE")) : (keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null)
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            shrinkResources true
            minifyEnabled true
        }
        debug {
            resValue "string", "app_name", "FlowAI"
        }
    }

    buildFeatures {
        buildConfig = true
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a','arm64-v8a','x86_64'
            universalApk false
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1"
}

flutter {
    source = "../.."
}
