plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("key.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace = "com.example.flow_ai"
    compileSdk = 34
    ndkVersion = flutter.ndkVersion
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId = "ai.flow.app"
        minSdkVersion flutter.minSdkVersion
        targetSdk = 34
        versionCode = flutter.versionCode
        versionName = flutter.versionName

        // Inject Supabase URL and anon key from local.properties into BuildConfig
        def props = new Properties()
        def propsFile = rootProject.file("local.properties")
        if (propsFile.exists()) {
            props.load(new FileInputStream(propsFile))
        }
        // Fallbacks ensure release builds work even if local.properties is absent (CI, different machine)
        def supabaseUrl = props.getProperty("SUPABASE_URL", "https://mesxkddjgnmmlwsxzxgo.supabase.co")
        def supabaseAnon = props.getProperty("SUPABASE_ANON_KEY", "eyJhbGciOiJIUzI1NiIsInR5cCI6IkpXVCJ9.eyJpc3MiOiJzdXBhYmFzZSIsInJlZiI6Im1lc3hrZGRqZ25tbWx3c3h6eGdvIiwicm9sZSI6ImFub24iLCJpYXQiOjE3NTQ3NTgzODQsImV4cCI6MjA3MDMzNDM4NH0.sitPOrrlf6KiIuSDjwz4wezrkm2nR65_Mts4op5Os-c")
        buildConfigField "String", "SUPABASE_URL", '"' + supabaseUrl + '"'
        buildConfigField "String", "SUPABASE_ANON_KEY", '"' + supabaseAnon + '"'
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            shrinkResources true
            minifyEnabled true
        }
        debug {
            resValue "string", "app_name", "FlowAI"
        }
    }

    buildFeatures {
        buildConfig = true
    }
    splits {
        abi{
            enable true
            reset()
            include 'armeabi-v7a','arm64-v8a','x86_64'
            universalApk false
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1"
}

flutter {
    source = "../.."
}
